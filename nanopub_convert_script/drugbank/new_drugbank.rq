#--------------------------------------------------------------------------------------------
# version 1.0
# in assertion graphs: 
#  - add sio:hasFullName (this is not in SIO ontology) to provide full name for each protein
#    protein's labels are gene-name abbreviations
#  - add sio:action (made up)
#############################################################################################

# Drugbank dataset contains total 15107 drug-target interactions. 
# 13540 interactions have drug, target, pubmed at the same time. 
# 5424 interactions (including duplicated ones) have dv:action predicate. 
# duplicated ones eg: 
# <http://bio2rdf.org/drugbank_resource:DB00734_BE0000572> has two actions: agonist and antagnist

prefix fn: <http://www.w3.org/2005/xpath-functions#>
PREFIX pav: <http://purl.org/pav/2.0/>
prefix sio: <http://semanticscience.org/resource/>
prefix prov: <http://www.w3.org/ns/prov#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
prefix nanopub: <http://www.nanopub.org/nschema#>


insert{
	graph ?nanopub{
        ?nanopub a nanopub:Nanopublication ;
                 nanopub:hasAssertion ?assertion ;
                 nanopub:hasProvenance ?provenance ;
                 nanopub:hasPublicationInfo ?publicationInfo .
 
        ?provenance nanopub:hasAttribution ?attribution .
 
        ?assertion a nanopub:Assertion .
        ?provenance a nanopub:Provenance .
        ?publicationInfo a nanopub:publicationInfo .
        ?attribution a nanopub:Attribution .
	}
	graph ?assertion{
        ?interaction a <http://bio2rdf.org/drugbank_vocabulary:Target-Relation>.
        ?interaction sio:has-participant ?drug.
        ?interaction sio:has-target ?target_uniprot.
        ?interaction sio:action ?action. 
        ?drug a sio:drug.
        ?drug rdfs:label ?drug_label.
        ?drug <http://bio2rdf.org/bio2rdf_vocabulary:uri> ?drug_url.
        ?target_uniprot a sio:protein.
        ?target_uniprot <http://www.w3.org/2000/01/rdf-schema#label> ?target_abbreviation.
        ?target_uniprot <http://bio2rdf.org/bio2rdf_vocabulary:uri> ?target_url.
        ?target_uniprot sio:hasFullName ?target_label.
	}
	graph ?provenance{
        ?assertion prov:wasGeneratedBy [   
                    a prov:Activity
                    ].
        ?assertion sio:SIO_000772 ?pubmed . 
	}
	graph ?attribution{
	    ?assertion prov:wasQuotedFrom <http://download.bio2rdf.org/current/drugbank/>.
	    <http://download.bio2rdf.org/current/drugbank/> rdfs:label "Drugbank".
	}
	graph ?publicationInfo{
        ?nanopub pav:authoredBy <http://tw.rpi.edu/web/person/RuiYan> ;
                 pav:createdBy <http://tw.rpi.edu/web/person/RuiYan> ;
                 dcterms:created ?now ;
                 dcterms:rights <http://opendatacommons.org/licenses/odbl/1.0/> ; 
                 dcterms:rightsHolder <http://tw.rpi.edu> .
	}
}

where {
  SERVICE <http://localhost:8890/sparql> {
    ?interaction a <http://bio2rdf.org/drugbank_vocabulary:Target-Relation>.
    ?interaction <http://bio2rdf.org/drugbank_vocabulary:drug> ?drug.
    ?interaction <http://bio2rdf.org/drugbank_vocabulary:target> ?target. 
    ?interaction <http://bio2rdf.org/drugbank_vocabulary:reference> ?pubmed. 
    optional {
      ?interaction <http://bio2rdf.org/drugbank_vocabulary:action> ?action. 
      ?drug <http://www.w3.org/2000/01/rdf-schema#label> ?drug_label.
      ?drug <http://bio2rdf.org/bio2rdf_vocabulary:uri> ?drug_url.
      ?target <http://bio2rdf.org/drugbank_vocabulary:gene-name> ?target_abbreviation.
      ?target <http://www.w3.org/2000/01/rdf-schema#label> ?target_label.
      ?target <http://bio2rdf.org/bio2rdf_vocabulary:uri> ?target_url.
      ?target <http://bio2rdf.org/drugbank_vocabulary:x-uniprot> ?target_uniprot
    }
    
    BIND(iri(fn:concat(?interaction,"/nanopub")) as ?nanopub)
    BIND(iri(fn:concat(?interaction,"/nanopub/assertion")) as ?assertion)
    BIND(iri(fn:concat(?interaction,"/nanopub/provenance")) as ?provenance)
    BIND(iri(fn:concat(?interaction,"/nanopub/attribution")) as ?attribution)
    BIND(iri(fn:concat(?interaction,"/nanopub/publicationInfo")) as ?publicationInfo)
    BIND(NOW() as ?now)
  }
}