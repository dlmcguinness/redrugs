PREFIX np: <http://www.nanopub.org/nschema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX pav: <http://purl.org/pav/2.0/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX iv: <http://bio2rdf.org/irefindex_vocabulary:>
PREFIX prov: <http://www.w3.org/ns/prov#>

insert{
	GRAPH ?nanopub
	{
		?nanopub a np:Nanopublication .
		?nanopub np:hasAssertion ?assertionGraph .
		?assertionGraph a np:Assertion .
		?nanopub np:hasProvenance ?provenanceGraph .
		?provenanceGraph a np:Provenance .
		?nanopub np:hasPublicationInfo ?publicationInfoGraph .
		?publicationInfoGraph a np:PublicationInfo .
	}
	GRAPH ?assertionGraph
	{
		?interaction a sio:SIO_000897 . #SIO_000897 means association
        ?interaction a ?interaction_type .
        ?interaction iv:interactor_a ?a .
        ?a a ?a_type .
        ?a_type <http://purl.org/dc/terms/title> ?a_type_title .
        ?interaction iv:interactor_b ?b .
        ?b a ?b_type .
        ?b_type <http://purl.org/dc/terms/title> ?b_type_title .
	}
	GRAPH ?provenanceGraph
	{
        ?assertionGraph prov:wasGeneratedBy [   
                             a prov:Activity, ?method
        ].
		?assertionGraph sio:SIO_000772 ?article . # specific paper
		?assertionGraph sio:SIO_000253 ?dataset . # which dataset it comes from
	}
	GRAPH ?publicationInfoGraph
	{
		?nanopub pav:authoredBy <http://tw.rpi.edu> ;
		 pav:createdBy <http://tw.rpi.edu> ;
		 dcterms:created ?now ;
		 dcterms:rights <http://opendatacommons.org/licenses/odbl/1.0/> ; #which license to use ?
		 dcterms:rightsHolder <http://tw.rpi.edu> .
	}
}
where{
  { service <http://cu.irefindex.bio2rdf.org/sparql> {
    select distinct *{
        ?interaction a ?interaction_type .
        filter regex(?interaction_type, "^.*?(?<!Resource)$", "i") # filter out all of the other types ending "Resource" 
        ?interaction iv:interactor_a ?a .
        ?a a ?a_type .
        ?a_type <http://purl.org/dc/terms/title> ?a_type_title .
        ?interaction iv:interactor_b ?b .
        ?b a ?b_type .
        ?b_type <http://purl.org/dc/terms/title> ?b_type_title .
        ?interaction iv:method ?method .
        ?interaction iv:article ?article .
        ?interaction <http://rdfs.org/ns/void#inDataset> ?dataset .
      }
    } 
  }
    BIND(STRAFTER(str(?interaction),"http://bio2rdf.org/") AS ?id)
    BIND(iri(concat('http://tw.rpi.edu/redrugs/', str(?id))) as ?base)
  	BIND(iri(concat(str(?interaction),"_nanopub")) as ?nanopub)
	BIND(iri(concat(str(?nanopub),"_assertion")) as ?assertionGraph)
	BIND(iri(concat(str(?nanopub),"_provenance")) as ?provenanceGraph)
	BIND(iri(concat(str(?nanopub),"_publicationInfo")) as ?publicationInfoGraph)
	BIND(NOW() as ?now)
}
