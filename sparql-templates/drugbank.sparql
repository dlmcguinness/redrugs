PREFIX np: <http://www.nanopub.org/nschema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX pav: <http://purl.org/pav/2.0/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dv: <http://bio2rdf.org/drugbank_vocabulary:>

insert{
	GRAPH ?nanopub
	{
		?nanopub a np:Nanopublication .
		?nanopub np:hasAssertion ?assertionGraph .
		?assertionGraph a np:Assertion .
		?nanopub np:hasProvenance ?provenanceGraph .
		?provenanceGraph a np:Provenance .
		?nanopub np:hasPublicationInfo ?publicationInfoGraph .
		?publicationInfoGraph a np:PublicationInfo .
	}
	GRAPH ?assertionGraph
	{
		?association a sio:SIO_000897 . #SIO_000897 means association, cannot find a specific SIO term for drug-target relation
		?association sio:SIO_000628 ?drug .
		?drug a dv:Drug .
		?association sio:SIO_000628 ?target .
		?target a dv:Target .
		?association dv:action ?action . 
        ?action a <http://bio2rdf.org/drugbank_vocabulary:Resource> .
        ?association <http://bio2rdf.org/drugbank_vocabulary:action> ?action . 
        ?action a <http://bio2rdf.org/drugbank_vocabulary:Resource> .

	}
	GRAPH ?provenanceGraph
	{
		?assertionGraph sio:SIO_000772 ?reference . # specific paper
		?assertionGraph sio:SIO_000253 ?source . # which dataset it comes from
	}
	GRAPH ?publicationInfoGraph
	{
		?nanopub pav:authoredBy <http://tw.rpi.edu> ;
		 pav:createdBy <http://tw.rpi.edu> ;
		 dcterms:created ?now ;
		 dcterms:rights <http://opendatacommons.org/licenses/odbl/1.0/> ; #which license to use ?
		 dcterms:rightsHolder <http://tw.rpi.edu> .
	}
}
where{
  { service <http://cu.drugbank.bio2rdf.org/sparql> {
      select distinct *{
        ?association <http://bio2rdf.org/drugbank_vocabulary:drug> ?drug .
        ?drug a <http://bio2rdf.org/drugbank_vocabulary:Drug> .
        optional { ?drug <http://bio2rdf.org/drugbank_vocabulary:x-uniprot> ?drug_uniprot .}
        ?association <http://bio2rdf.org/drugbank_vocabulary:target> ?target .
        ?target a <http://bio2rdf.org/drugbank_vocabulary:Target> .
        optional { ?target <http://bio2rdf.org/drugbank_vocabulary:x-uniprot> ?target_uniprot .}
        ?association <http://bio2rdf.org/drugbank_vocabulary:action> ?action . 
        ?action a <http://bio2rdf.org/drugbank_vocabulary:Resource> .
        ?association <http://bio2rdf.org/drugbank_vocabulary:reference> ?reference . 
        ?reference a <http://bio2rdf.org/pubmed_vocabulary:Resource> .
        ?reference <http://rdfs.org/ns/void#inDataset> ?source.
	    # FILTER (?association = <http://bio2rdf.org/drugbank_resource:DB00247_BE0000650>) #for testing
      }
    } 
  }
    BIND(STRAFTER(str(?association),"http://bio2rdf.org/") AS ?id)
    BIND(iri(concat('http://tw.rpi.edu/redrugs/', str(?id))) as ?base)
  	BIND(iri(concat(str(?association),"_nanopub")) as ?nanopub)
	BIND(iri(concat(str(?nanopub),"_assertion")) as ?assertionGraph)
	BIND(iri(concat(str(?nanopub),"_provenance")) as ?provenanceGraph)
	BIND(iri(concat(str(?nanopub),"_publicationInfo")) as ?publicationInfoGraph)
	BIND(NOW() as ?now)
}

